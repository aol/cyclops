import org.gradle.api.artifacts.maven.MavenDeployment

buildscript {
	repositories {
		jcenter()
		
	}

	dependencies {
		classpath 'com.bmuschko:gradle-nexus-plugin:2.2'
	
	}
}
plugins {
	id 'me.champeau.gradle.jmh' version '0.2.0'
 }
def custom ={ "$rootDir/gradle/${it}.gradle"} //find custom plugins locally

 
apply plugin: 'me.champeau.gradle.jmh'
apply plugin: 'java'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'com.bmuschko.nexus'
apply plugin: 'jacoco'
apply from: custom('jacoco-version')

sourceCompatibility = 1.8

jar {
    manifest {
        attributes 'Implementation-Title': 'simple-react', 'Implementation-Version': version
    }
}

apply from: custom('jacoco-config')
apply from: custom('jacoco-excludes')


repositories {
    mavenCentral()
	jcenter()

}

if (JavaVersion.current().isJava8Compatible()) {
    allprojects {
      tasks.withType(Javadoc) {
        options.addStringOption('Xdoclint:none', '-quiet')
      }
    }
  }
 

configurations {
	provided
}
dependencies {

	//compile group: 'org.derive4j.hkt', name: 'hkt', version: derive4jHKTVersion
	compile 'com.nurkiewicz.asyncretry:asyncretry:'+asyncRetryVersion
	compile 'org.pcollections:pcollections:'+pCollectionsVersion
	compile "org.jooq:jool:${joolVersion}"
	compile 'org.agrona:Agrona:'+agronaVersion
	compile 'org.reactivestreams:reactive-streams:'+reactiveStreamsVersion
	provided group: 'org.projectlombok', name: 'lombok', version:"${lombokVersion}"
	compile group: 'org.slf4j', name: 'jcl-over-slf4j', version:'1.7.13'
    compile group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.13'
    testCompile 'com.github.stefanbirkner:system-rules:1.10.0'
    testCompile 'io.projectreactor:reactor-core:2.5.0.M2'
	testCompile  'com.google.guava:guava:19.0'
    testCompile 'com.google.guava:guava-testlib:19.0'
	testCompile 'org.reactivestreams:reactive-streams-tck:1.0.0'
	testCompile group: 'junit', name: 'junit', version: '4.12'
	testCompile group: 'org.hamcrest', name: 'hamcrest-all', version:hamcrestVersion
	testCompile group: 'org.mockito', name: 'mockito-all', version:'1.9.5'
	testCompile 'io.vertx:vertx-core:3.0.0'
	testCompile group: 'org.testng', name: 'testng', version:'5.14.10'
    testCompile 'io.javaslang:javaslang:2.0.2'

	
	

}

sourceSets {
	main.compileClasspath += [configurations.provided]
	test.compileClasspath += [configurations.provided]
	test.runtimeClasspath += [configurations.provided]
}
jacocoTestReport {
    afterEvaluate {
        classDirectories = files(classDirectories.files.collect {
            fileTree(dir: it, exclude: ['com/aol/cyclops2/util/stream/scheduling/cron/**'])
        })
    }
}
jmh {
	jmhVersion = '1.14'
	humanOutputFile = null
	/**
	if (project.hasProperty('jmh')) {
		include = ".*" + project.jmh + ".*"
	} else {
		include = ".*"
	}
	 **/
	include = ".*"
}


task testNG(type: Test) {
    useTestNG {}
    reports.html.destination = file("$buildDir/reports/test")
}


task testSuite(type: Test) {

    include '**/*TestSuite.class'
    reports.junitXml.destination = "$buildDir/reports/test"
    reports.html.destination = "$buildDir/reports/test"
 }

 

test {
        reports.html.destination = file("$buildDir/reports/test")
    
        dependsOn testNG
     //   dependsOn testSuite
        forkEvery = 5
        
        testLogging {
            events "started","passed", "skipped", "failed"//, "standardOut", "standardError"
        }
}
eclipse.classpath.plusConfigurations += [configurations.provided]  // Eclipse users only

test {
    systemProperties 'property': 'value'
}

javadoc {
    configure((CoreJavadocOptions) getOptions()) {
        addStringOption('sourcepath', "/Sources/jool-0.9.11-sources.jar")
    }
    exclude '**/internal/**'
}
modifyPom {
	project {
		name 'cyclops2-react'
		description 'Platform for Functional Reactive Programming with Java 8'
		url 'https://github.com/aol/simple-react'
		inceptionYear '2015'

		groupId 'com.aol.simplereact'
		artifactId 'cyclops-react'
		version "$version"
		
		scm {
			url 'scm:git@github.com:aol/simple-react.git'
            connection 'scm:git@github.com:aol/simple-react.git'
            developerConnection 'scm:git@github.com:aol/simple-react.git'
		}

		licenses {
			license {
				name 'The Apache Software License, Version 2.0'
				url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
				distribution 'repo'
			}
		}

		developers {
			developer {
				id 'johnmcclean-aol'
				name 'John McClean'
				email 'john.mcclean@teamaol.com'
			}
			developer {
				id 'kewangie'
				name 'Ke Wang'
				email 'ke.wang@teamaol.com'
			}

			developer {
				id 'tkountis'
				name 'Thomas Kountis Wang'
				email 'thomas.kountis@adtech.com'
			}
			developer {
				id 'colinfkennedy'
				name 'Colin Kennedy'
				email 'Colin.Kennedy@teamaol.com'
			}
		}
	}
}

extraArchive {
	sources = true
	tests = true
	javadoc = true
}

nexus {
	sign = true
	repositoryUrl = 'https://oss.sonatype.org/service/local/staging/deploy/maven2'
	snapshotRepositoryUrl = 'https://oss.sonatype.org/content/repositories/snapshots'
}
